
import org.apache.commons.io.FilenameUtils

buildscript {
    repositories {
        ivy {
            name "ivyLocal"
            url "${System.getProperty('user.home')}/.ivy/repo"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.mylonelybear:MlbGradlePlugins:latest.integration'
        classpath 'commons-io:commons-io:2.4'
    }
}

ext {
    buildDir = 'build'
    ansibleBuildDir = "$buildDir/ansible"
    baseBuildDir = "$buildDir/base"
    boxBuildDir = "$buildDir/box"
    packerBuildDir = "$buildDir/packer"
    vagrantBuildDir = "$buildDir/vagrant"

    srcDir = 'src/main'
    ansibleSrcDir = "$srcDir/ansible"
    packerSrcDir = "$srcDir/packer"

    distDir = 'dist'
}

def ansibleCommonDir = file(ansibleSrcDir)
def packerSrcFile = file('src/main/packer/packer.json')
def provisionSrcFile = file('src/main/packer/provision.sh')

allprojects {
    apply plugin: 'mylonelybear'
    defaultTasks 'dist'
}

subprojects { sp ->
    version = 'dev'

    configurations {
        base
        virtualbox
        vagrant
    }

    task clean << {
        delete buildDir
    }

    task prepare() {
        outputs.dir baseBuildDir
        outputs.dir ansibleBuildDir

        inputs.files configurations.base
        inputs.files packerSrcFile
        inputs.files provisionSrcFile
        inputs.dir ansibleSrcDir
        inputs.dir ansibleCommonDir

        doLast {
            copy {
                from zipTree(configurations.base.asPath)
                into baseBuildDir
            }
            
            file(ansibleBuildDir).mkdirs()

            copy {
                from ansibleCommonDir
                into ansibleBuildDir
            }

            copy {
                from ansibleSrcDir
                into ansibleBuildDir
            }
        }
    }

    task build(dependsOn: prepare) {
        inputs.file prepare.outputs.files
        inputs.file packerSrcDir
        outputs.dir packerBuildDir
        outputs.dir vagrantBuildDir
        outputs.dir boxBuildDir

        doLast {
            project.ext.baseBox = fileTree(dir: baseBuildDir, include: '*.ovf').singleFile.name

            copy {
                from packerSrcFile
                into packerBuildDir
                expand(project: project)
            }

            copy {
                from provisionSrcFile
                into packerBuildDir
            }

            delete boxBuildDir

            file(vagrantBuildDir).mkdirs()

            project.exec {
                executable 'packer'
                args 'build', "${packerBuildDir}/${packerSrcFile.name}"
            }
        }
    }

    task distVirtualBox(type: Zip, dependsOn: build) {
        baseName = project.name
        from boxBuildDir
        destinationDir = file(distDir)
    }

    task distVagrant(dependsOn: build) { 
        outputs.file "$distDir/${project.name}.box"

        doLast {
            copy {
                from vagrantBuildDir
                into distDir
            }
        }
    }

    task dist(dependsOn: [distVirtualBox, distVagrant])

    artifacts {
        virtualbox distVirtualBox
        vagrant distVagrant.outputs.files.singleFile
    }

    publish.dependsOn dist

    publishing {
        publications {
            ivy (IvyPublication) {
                artifact(distVirtualBox) {
                    type = 'virtualbox'
                }
            }
            ivy (IvyPublication) {
                artifact(distVagrant.outputs.files.singleFile) {
                    type = 'vagrant'
                }
            }
        }
    }
}


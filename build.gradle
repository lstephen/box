
import org.apache.commons.io.FilenameUtils

buildscript {
    repositories {
        ivy {
            name "ivyLocal"
            url "${System.getProperty('user.home')}/.ivy/repo"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.mylonelybear:MlbGradlePlugins:latest.integration'
        classpath 'commons-io:commons-io:2.4'
    }
}

ext {
    buildDir = 'build'
    ansibleBuildDir = "$buildDir/ansible"
    baseBuildDir = "$buildDir/base"
    boxBuildDir = "$buildDir/box"
    packerBuildDir = "$buildDir/packer"
    serverspecBuildDir = "$buildDir/serverspec"
    vagrantBuildDir = "$buildDir/vagrant"

    srcDir = 'src/main'
    ansibleSrcDir = "$srcDir/ansible"
    packerSrcDir = "$srcDir/packer"
    
    packerSrcFile = "$packerSrcDir/packer.json"

    distDir = 'dist'

    testDir = 'src/test'
    serverspecTestDir = "$testDir/serverspec"
}

def ansibleCommonDir = file(ansibleSrcDir)
def packerCommonSrcDir = file(packerSrcDir)
def packerCommonSrcFile = file(packerSrcFile)
def serverspecTestCommonDir = file(serverspecTestDir)

allprojects {
    apply plugin: 'mylonelybear'
    defaultTasks 'dist'
}

def hasBaseBox(project) {
    project.configurations.base.asPath != ''
}

subprojects { sp ->
    version = 'dev'

    configurations {
        base
        virtualbox 
        vagrant
    }


    task clean << {
        delete buildDir
    }

    task prepare() {
        outputs.dir baseBuildDir
        outputs.dir ansibleBuildDir

        inputs.files configurations.base
        inputs.dir ansibleSrcDir
        inputs.dir ansibleCommonDir

        doLast {
            if (hasBaseBox(sp)) {
                copy {
                    from zipTree(configurations.base.asPath)
                    into baseBuildDir
                }
            }
            
            file(ansibleBuildDir).mkdirs()

            copy {
                from ansibleCommonDir
                into ansibleBuildDir
            }

            copy {
                from ansibleSrcDir
                into ansibleBuildDir
            }
        }
    }

    task build(dependsOn: prepare) {
        inputs.file prepare.outputs.files
        inputs.dir packerCommonSrcDir
        inputs.dir packerSrcDir
        outputs.dir packerBuildDir
        outputs.dir vagrantBuildDir
        outputs.dir boxBuildDir

        doLast {
            project.ext.baseBox = hasBaseBox(sp) \
                  ? fileTree(dir: baseBuildDir, include: '*.ovf')
                      .singleFile
                      .name
                  : ''

            copy {
                from packerCommonSrcDir
                into packerBuildDir
            }

            copy {
                from packerCommonSrcFile
                into packerBuildDir
                expand(project: project)
            }

            copy {
                from packerSrcDir
                into packerBuildDir
            }

            copy {
                from packerSrcFile
                into packerBuildDir
                expand(project: project)
            }

            delete boxBuildDir

            file(vagrantBuildDir).mkdirs()

            exec {
                executable 'packer'
                args 'build', "${packerBuildDir}/${file(packerSrcFile).name}"
            }
        }
    }

    task distVirtualBox(type: Zip, dependsOn: build) {
        baseName = project.name
        from boxBuildDir
        destinationDir = file(distDir)
    }

    task distVagrant(dependsOn: build) { 
        inputs.dir vagrantBuildDir
        outputs.file "$distDir/${project.name}.box"

        doLast {
            copy {
                from vagrantBuildDir
                into distDir
            }
        }
    }

    task installVagrant(type: Exec, dependsOn: distVagrant) {
        executable 'vagrant'
        args 'box', 'add', "$distDir/${project.name}.box", '--name', "${project.name}", '--force'
    }


    task dist(dependsOn: [distVirtualBox, distVagrant])

    artifacts {
        virtualbox distVirtualBox
        vagrant distVagrant.outputs.files.singleFile
    }

    publish.dependsOn dist

    publishing {
        publications {
            ivy (IvyPublication) {
                artifact(distVirtualBox) {
                    type = 'virtualbox'
                }
            }
            ivy (IvyPublication) {
                artifact(distVagrant.outputs.files.singleFile) {
                    type = 'vagrant'
                }
            }
        }
    }

    task test(dependsOn: installVagrant) << {
        delete serverspecBuildDir
        file(serverspecBuildDir).mkdirs()

        copy {
            from serverspecTestCommonDir
            into serverspecBuildDir
            expand(project: project)
        }

        copy {
            from serverspecTestDir
            into serverspecBuildDir
        }

        exec {
            executable 'ruby'
            args '-S', 'rspec', '--default-path', '.', '-I', '.'
            workingDir file(serverspecBuildDir)
        }

        exec {
            executable 'vagrant'
            args 'halt'
            workingDir file(serverspecBuildDir)
        }

        exec {
            executable 'vagrant'
            args 'destroy', '-f'
            workingDir file(serverspecBuildDir)
        }
    }
}

